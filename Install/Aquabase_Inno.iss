; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Aquabase 2020"
#define MyAppVersion "8.4.5.394"
#define MyAppPublisher "Lazarus Projects"
#define MyAppURL "https://aquabase.blecher.co.za/wp"
#define MyAppExeName "Aquabase.exe"
#define MyAppUpdatesURL "ftp://ftp.blecher.co.za"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{718678DF-6B6F-4F46-9A26-4B971CF1DF33}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} - Build {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppUpdatesURL}
VersionInfoVersion={#MyAppVersion}
DefaultDirName={commonpf}\{#MyAppName}
DefaultGroupName={#MyAppName}
LicenseFile=C:\Users\Immo\Documents\Aquabase_x86_64\License.txt
OutputDir=C:\Users\Immo\Documents\LazarusProjects\Install
OutputBaseFilename=Aquabase_Setup
SetupIconFile=Z:\home\immo\Development\LazarusProjects\Aquacode\Icons\Aquabase.ico
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin
AllowUNCPath=no
DisableWelcomePage=no
AlwaysRestart=yes
InfoBeforeFile=C:\Users\Immo\Documents\LazarusProjects\Install\readme.txt
;prevent InnoSetup recalling the last used installation groupname and folder.
UsePreviousGroup=no
UsePreviousAppDir=no
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
; On all other architectures it will install in "32-bit mode".
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "install_qgis_plugin"; Description: "Install QGIS Plugin"; GroupDescription: "Optional Applications:"; Flags: unchecked;
Name: "install_vcredist64"; Description: "Install Microsoft Visual C Redistributable 2015 Update 3 RC (64Bit)"; GroupDescription: "Optional Applications:"; Flags: unchecked;
Name: "install_vcredist32"; Description: "Install Microsoft Visual C Redistributable 2015 Update 3 RC (32Bit)"; GroupDescription: "Optional Applications:"; Flags: unchecked;

[Files]
;32 Bit Stuff, logos, settings databases and license
Source: "C:\Users\Immo\Documents\Aquabase_i386\*.*"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist; Check: not Is64BitInstallMode
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\*.sqlite"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist; Check: not Is64BitInstallMode
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\map*.*"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist; Check: not Is64BitInstallMode
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\License.*"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist; Check: not Is64BitInstallMode
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\splash_logo.*"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist; Check: not Is64BitInstallMode
;64 Bit Stuff
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\*"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist; Check: Is64BitInstallMode
;All other general Stuff
Source: "C:\Users\Immo\Documents\LazarusProjects\Install\readme.txt"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\*.sqlite"; DestDir: "{app}\defaults"; Flags: ignoreversion
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\map*.*"; DestDir: "{app}\defaults"; Flags: ignoreversion
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\defaults\*"; DestDir: "{app}\defaults"; Flags: ignoreversion onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\proj\*"; DestDir: "{app}\proj"; Flags: ignoreversion onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\Templates\*"; DestDir: "{app}\Templates"; Flags: ignoreversion onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\BitMaps\*"; DestDir: "{app}\Bitmaps"; Flags: ignoreversion onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\Databases\*"; DestDir: "{app}\Databases"; Flags: ignoreversion recursesubdirs createallsubdirs onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\TimeZones\*"; DestDir: "{app}\TimeZones"; Flags: ignoreversion onlyifdoesntexist
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\Optional\vc_redist.x64.exe"; DestDir: "{app}\Optional"; Flags: ignoreversion onlyifdoesntexist; Check: WizardIsTaskSelected('install_vcredist64'); AfterInstall: MyAfterInstallVCredist64
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\Optional\vc_redist.x86.exe"; DestDir: "{app}\Optional"; Flags: ignoreversion onlyifdoesntexist; Check: WizardIsTaskSelected('install_vcredist32'); AfterInstall: MyAfterInstallVCredist32
Source: "C:\Users\Immo\Documents\Aquabase_x86_64\plugins\*"; DestDir: "{app}\plugins\"; Flags: recursesubdirs ignoreversion onlyifdoesntexist; Check: WizardIsTaskSelected('install_qgis_plugin')
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

;[FontInstall]
Source: "c:\windows\fonts\liberationmono-regular.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Mono"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationmono-bold.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Mono Bold"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationmono-boldItalic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Mono Bold Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationmono-italic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Mono Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsans-regular.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsans-bold.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Bold"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsans-boldItalic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Bold Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsans-italic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsansnarrow-Regular.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Narrow"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsansnarrow-Bold.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Narrow Bold"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsansnarrow-BoldItalic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Narrow Bold Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationsansnarrow-Italic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Sans Narrow Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationserif-regular.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Serif"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationserif-bold.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Serif Bold"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationserif-boldItalic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Serif Bold Italic"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "c:\windows\fonts\liberationserif-italic.ttf"; DestDir: "{commonfonts}"; FontInstall: "Liberation Serif Italic"; Flags: onlyifdoesntexist uninsneveruninstall

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "PROJ_LIB"; ValueData: "{app}\proj"
Root: HKLM; Subkey: "Software\Lazarus Projects"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Lazarus Projects\Aquabase"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\Lazarus Projects\Aquabase\SystemPaths"; ValueType: string; ValueName: "InstallDir"; ValueData: "{app}"
Root: HKLM; Subkey: "Software\Licenses"; ValueType: string; ValueName: "718678DF-6B6F-4F46-9A26-4B971CF1DF33"; ValueData: "{#MyAppVersion}"; Flags: createvalueifdoesntexist

[Run]
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
procedure MyAfterInstallVCredist64();
var
  ResultCode: Integer;
begin
  if WizardIsTaskSelected('install_vcredist64') then
  begin
    Exec(ExpandConstant('{app}\Optional\vc_redist.x64.exe'), '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
  end;
end;

procedure MyAfterInstallVCredist32();
var
  ResultCode: Integer;
begin
  if WizardIsTaskSelected('install_vcredist32') then
  begin
    Exec(ExpandConstant('{app}\Optional\vc_redist.x86.exe'), '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
  end;
end;
